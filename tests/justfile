_default:
    @just --list --unsorted
    
# Run all tests
test:
    tt run --root=.. -j8

# Make PDF of persistent tests for visual inspection
pdf:
    #!/usr/bin/env bash
    shopt -s nullglob
    set -eu
    # Process all persistent tests (those with a ref/ dir) 
    for file in */ref/; do
        just _make-pdf "${file%/ref/}/test.typ"
    done

_make-pdf filename: _pdf-dir
    typst c --root=.. {{filename}} pdf/{{parent_directory(filename)}}.pdf

_pdf-dir:
    @mkdir -p pdf
    
# Remove unnecessary files/directories such as .ipynb_checkpoints
clean:
    #!/usr/bin/env bash
    rm -rf */.ipynb_checkpoints
